#!/usr/bin/perl -w
#  $Id$  -*-perl-*-
package Para;

#=====================================================================
#
# DESCRIPTION
#   Paranormal.se creation
#
# AUTHOR
#   Jonas Liljegren   <jonas@paranormal.se>
#
# COPYRIGHT
#   Copyright (C) 2004 Jonas Liljegren.  All Rights Reserved.
#
#   This module is free software; you can redistribute it and/or
#   modify it under the same terms as Perl itself.
#
#=====================================================================

BEGIN
{
    our $VERSION  = sprintf("%d.%02d", q$Revision$ =~ /(\d+)\.(\d+)/);
    print "Starting creation $VERSION\n";

    use FindBin;
    my $configfile = $FindBin::Bin . "/config.pl";
    do $configfile or die "Failed to load config $configfile: $! ($@)\n";
}

use strict;

our $PARAFRAME; # Dir without ending /
our $APPROOT;   # Dir without ending /
our $SITE_CFG;  # Hashref with rest of config

use lib "$PARAFRAME/lib";
use lib "$APPROOT/lib";

use Para::Frame;
use Para::Frame::DBIx;
use Para::Frame::Time qw( now );
use Para::Frame::Email;
use Para::Frame::Email::Address;
use Para::Frame::Utils qw( chmod_tree );
use Para::Frame::Watchdog;

use Para;
use Para::Member;

{
    # Do not daemonize if run with cmdline argument
    my $daemonize = @ARGV ? 0 : 1;
    my $watchdog = 1;   # Use watchdog

    unless( $SITE_CFG )
    {
	die "Site configuration missing";
    }

    my $cfg =
    {
	'paraframe' => $PARAFRAME,
	'paraframe_group' => 'psi_cgi',

	'appbase'      => 'Para',
	'approot'      => $APPROOT,

	'user_class'   => 'Para::Member',
	'bg_user_code' => sub{ Para::Member->skapelsen },
	'logfile'      => "$APPROOT/logs/creation.log",
	'pidfile'      => "$APPROOT/logs/creation.pid",

	port           => $SITE_CFG->{'port'},
	debug          => $ARGV[0] || $SITE_CFG->{'debug'} || 0,

	'site'    =>
	{
	    # Path in  URL to website
	    'webhome'    => '',
	    'webhost'    => $SITE_CFG->{'webhost'},
	    'last_step'  => "/member/db/",
	    'static_ttc' => "$PARAFRAME/var/ttc/psidb2",
	}

    };
    Para::Frame->configure( $cfg );

    # Make sure we can read/write generated files
    chmod_tree( "$PARAFRAME/var/ttc" );
    chmod_tree( "$APPROOT/topic",
		{
		    dirmode => 02775,
		    filemode => 0664,
		});

    # Configure database
    #
    $Para::dbix = Para::Frame::DBIx ->
	new({
	    connect => $SITE_CFG->{'dbconnect'},
	    bind_dbh => \ $Para::dbh,
	});


    # Define TT filters
    #
    Para::Frame->add_tt_filters('html', {
	'html_psi' => \&Para::Widget::html_psi,
	'html_psi_nolinks' => sub{ Para::Widget::html_psi($_[0], 1) },
    });


    # Attatch code to hooks
    #

    # Just in case we temporarily switched to root and got an exception
    Para::Frame->add_hook('on_error_detect', sub
			  {
			      Para::Member->revert_from_temporary_user();
			    });

    Para::Frame->add_hook('before_user_logout', sub
			  {
			      $_[0]->on_logout;
			  });

    Para::Frame->add_hook('user_login', sub
			  {
			      $_[0]->on_login;
			  });
    Para::Frame->add_hook('before_db_commit', sub
			  {
			      Para::Member->commit();
			      Para::Topic->commit();
			  });
    Para::Frame->add_hook('after_db_rollback', sub
			  {
			      Para::Member->rollback();
			      Para::Topic->rollback();
			  });
    Para::Frame->add_hook('done', \&Para::Topic::publish_urgent);
    Para::Frame->add_hook('on_startup', sub
			  {
			      $Para::dbix->connect;

			      # Take shortcut...
			      return unless $Para::Frame::CFG->{'do_bgjob'};

			      # Setup MOTD
			      Para::Widget::new_entry(undef, 'static');
			      $Para::MOTD = Para::Widget::html_psi( Para::Member->skapelsen->presentation );
			  });
    Para::Frame->add_hook('add_background_jobs', \&Para::add_background_jobs);
    Para::Frame->add_hook('on_memory', \&Para::on_memory);
    Para::Frame->add_hook('busy_background_job', \&Para::clear_caches);


    my $global_params =
    {
     'select_persons'    => \&Para::Widget::select_persons,
     'diff'              => \&Para::Widget::diff,
     'tfilter_init'      => \&Para::Widget::tfilter_init,

     'select_persons_by_name' => sub{ Para::Member->by_name( @_ ) },
     'getmember'         => sub{ Para::Member->get_by_id( @_ ) },
     'getmember_by_nick' => sub{ Para::Member->get_by_nickname( @_ ) },
     'getmember_currently_online'
 	                 => sub{ Para::Member->currently_online( @_ ) },
     'count_online'      => sub{ Para::Member->count_currently_online },

     'topic'             => sub{ Para::Topic->get_by_id( @_ ) },
     'find_topic'        => sub{ Para::Topic->find( @_ ) },
     'find_one_topic'    => sub{ Para::Topic->find_one( @_ ) },

     'interest_tree'     => sub{ Para::Interest::Tree->new( @_ ) },

     'getarc'            => sub{ Para::Arc->get( @_ ) },
     'getarctype'        => sub{ Para::Arctype->new( @_ ) },
     'getarctype_list'   => sub{ Para::Arctype->list( @_ ) },

     'getalias_list'     => sub{ Para::Alias->find_by_name( @_ ) },

     'getevent'          => sub{ Para::Event->get_by_id( @_ ) },
     'getevent_list'     => sub{ Para::Event->list( @_ ) },

     'getpayment'        => sub{ Para::Payment->new( @_ ) },

     'age'               => sub{ localtime->year - $_[0]->{'bdate_ymd_year'} },
     'year'              => sub{ localtime->year },
     'now'               => sub{ scalar localtime },
     'date'              => sub{ Para::Frame::Time->get(@_) },
     'elapsed_time'      => sub{ Para::Frame::Utils::elapsed_time(@_) },

     'delay'             => sub{ sleep(@_) },
     'uc'                => sub{ "\U$_[0]" },
     'lc'                => sub{ "\L$_[0]" },

     'timediff'          => \&Para::Frame::Utils::timediff,
     'css'               =>
     {
      persistent => [ "/css/default.css" ],
      alternate =>
      {
       light => [ "/css/light.css" ],
       blue => [ "/css/blue.css" ],
      },
      default => 'blue',
     },
     'favicon'           => "/favicon.ico",
     'body_class'        => "meta",
     'motd'              => sub{ $Para::MOTD },
    };
    Para::Frame->add_global_tt_params( $global_params );

    if( $daemonize )
    {
        Para::Frame->daemonize( $watchdog );
    }
    elsif( $watchdog )
    {
	Para::Frame->watchdog_startup();
    }
    else
    {
	Para::Frame->startup();
	Para::Frame::main_loop();
    }
}

#########################################################
